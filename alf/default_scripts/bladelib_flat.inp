* CHARMM with BLaDE input file for lambda dynamics
*

set fnex = 5.5

stream "variablesflat.inp"

bomblev -1

stream "prep/@{SYSNAME}.inp"

! Set nonbonded options
open unit 101 read unform name prep/nbond.str
if ?IOSTAT .eq. 1 then ! 1 if does exist, -1 if does not exist
   stream prep/nbond.str
else
   echo "Error: cannot find prep/nbond.str"
   stop
endif

faster on

if @minimize .eq. 1 then ! only defined in variables1.inp
define uninitialized select .not. initialized end
if ?nsel .eq. 0 then
   ! domdec gpu only dlb off ndir 1 @nnodes 1
   mini sd nstep 100 nprint 10 step 0.005                  !! Do min, Steepest-Decent
   write psf card name prep/minimized.psf 
* after minimization psf
*
   write coor card name "prep/minimized.crd"
* patch crd file
*
else
   echo "Error: cannot create prep/minimized.crd because some atoms have unitialized coordinates"
   stop
endif
endif

bomblev -1
open unit 101 read unform name "prep/minimized.crd"
if ?IOSTAT .eq. 1 then ! 1 if does exist, -1 if does not exist
   read coor card resid name "prep/minimized.crd"
else
open unit 101 read unform name "prep/minimized.pdb"
if ?IOSTAT .eq. 1 then ! 1 if does exist, -1 if does not exist
   read coor pdb resid name "prep/minimized.pdb"
else
   echo "Error: cannot find prep/minimized.pdb or prep/minimized.crd"
   stop
endif
endif


!---------------------------------------------------------------------------
! Setup of dynamics run
!---------------------------------------------------------------------------

if @nreps .eq. 1 then
   set myrep = 0
else
   !! Initiate BP-REX MSLD
   set dph = 0.5
   calc sph = 7.0 - @dph*@ncentral
   repd nrep @nreps EXLM FREQuency 1000 MSPH SPH @sph DPH @dph unit 27

   !! Direct output of replicas to their individual files
   set myrep = ?myrep
   open write form unit 28 name output
   outu 28
   system "cp ../nbshift/vb.inp ../nbshift/vb.inp_@MYREP"
   stream "../nbshift/vb.inp"
endif

shake fast bonh param
scalar fbeta set 0.1 sele all end

! domdec gpu only dlb off ndir 1 @nnodes 1

!! Heating run
open unit 21 write unform name "dcd/@{SYSNAME}_heat.dcd"
open unit 22 write form name "res/@{SYSNAME}_heat.res"
open unit 24 write file name "res/@{SYSNAME}_heat.lmd"
if nreps .gt. 1 then
   open write form unit 27 name "@{SYSNAME}_heat.rex"
endif

if @?restartfile eq 0 then
set resttoken = start
set iurest = -1
else
set resttoken = restart
open unit 23 read form name "@{RESTARTFILE}"
set iurest = 23
endif

calc seed = @seed
calc pmass = ?natom * 0.12
dynamics cpt @resttoken -
  blade prmc iprs 100 pref 1 prdv 100 -
  iseed @seed @seed @seed @seed -
  timestep 0.002 -      !!timestep interval
  nstep @esteps -       !!no. of steps  (== 200 ps)
  nprint 1000 -         !!freq of printout
  iprfrq 1000 -         !!freq of calc avg/rms energy
  nsavc 1000 -          !!freq of writing coordinates
  nsavl 10 -            !!freq of writing lambda histograms
  isvfrq @esteps -      !!freq of writing out restart files
  iunread @iurest -     !!read restart file
  iuncrd 21 -           !!write out coordinates
  iunwri 22 -           !!write out restart file
  iunldm 24 -           !!write out lambda histograms and biasing potential
  firstt @temp -        !!initial temp
  finalt @temp -        !!final temp
  tstruc @temp -        !!temp at which starting structure has been equilibrated
  tbath @temp -         !!temp of langevin bath
  pconstant -           !! use constant pressure (pressure.doc recommendations)
  pmass @pmass -        !! pressure "mass" 2% of mass or 400.0 amu
  pref 1.0 -            !! pressure in atm
  pgamma 20.0 -         !! pressure collision frequency
  hoover -
  reft @temp -
  tmass 1000 -
  ichecw 0 -            !!do not scale velocities to final temp (i.e. equilibrate)
  ihtfrq 0 -            !!freq of heating
  ieqfrq 0 -            !!freq of scaling/assigning velocity(X)
  iasors 1 -            !!assign velocities during heating (0 will be scale velocities)
  iasvel 1 -            !!using gaussian distribution to assign velocities
  iscvel 0 -
  inbfrq -1 -
  ilbfrq 0 -
  imgfrq -1 -
  ntrfrq 0 - ! 500 -
  echeck -1             !!energy tolerance check before crashing

!! Production flattening run
open unit 21 write unform name "dcd/@{SYSNAME}_flat.dcd"
open unit 22 write form name "res/@{SYSNAME}_flat.res"
! open unit 23 read form name "res/@{SYSNAME}_heat.res"
open unit 24 write file name "res/@{SYSNAME}_flat.lmd"
if nreps .gt. 1 then
   open write form unit 27 name "@{SYSNAME}_flat.rex"
endif

calc pmass = ?natom * 0.12
dynamics cpt start -
  blade prmc iprs 100 pref 1 prdv 100 -
  timestep 0.002 -      !!timestep interval
  nstep  @nsteps -      !!no. of steps  (== 2 ns)
  nprint 1000 -         !!freq of printout
  iprfrq 1000 -         !!freq of calc avg/rms energy
  nsavc 1000 -          !!freq of writing coordinates
  nsavl 10 -            !!freq of writing lambda histograms
  isvfrq @nsteps -      !!freq of writing out restart files
  iunread -1 - ! 23 -   !!read restart file
  iuncrd 21 -           !!write out coordinates
  iunwri 22 -           !!write out restart file
  iunldm 24 -           !!write out lambda histograms and biasing potential
  firstt @temp -        !!initial temp
  finalt @temp -        !!final temp
  tstruc @temp -        !!temp at which starting structure has been equilibrated
  tbath @temp -         !!temp of langevin bath
  pconstant -           !! use constant pressure (pressure.doc recommendations)
  pmass @pmass -        !! pressure "mass" 2% of mass or 400.0 amu
  pref 1.0 -            !! pressure in atm
  pgamma 20.0 -         !! pressure collision frequency
  hoover -
  reft @temp -
  tmass 1000 -
  ichecw 0 -            !!do not scale velocities to final temp (i.e. equilibrate)
  ihtfrq 0 -            !!freq of heating
  ieqfrq 0 -            !!freq of scaling/assigning velocity(X)
  iasors 1 -            !!assign velocities during heating (0 will be scale velocities)
  iasvel 1 -            !!using gaussian distribution to assign velocities
  iscvel 0 -
  inbfrq -1 -
  ilbfrq 0 -
  imgfrq -1 -
  ntrfrq 0 - ! 500 -
  echeck -1             !!energy tolerance check before crashing

!----------------------------------------------------------------------------
! Write out structure after simulation
!----------------------------------------------------------------------------
! write psf card name "@{SYSNAME}_flat.psf"
! * after dynamics psf
! *
! write coor pdb name "@{SYSNAME}_flat.pdb"
! * after dynamics pdb
! *

!---------------------------------------------------------------------------
! Analyze l-dynamics output
!---------------------------------------------------------------------------
! open unit 44 read file name "res/@{SYSNAME}_flat.lmd"
! 
! traj lamb print ctlo 0.8 cthi 0.95 first 44 nunit 1

stop

